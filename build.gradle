buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}") 
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = 'spring-boot-polymer'
}

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-devtools')

    testCompile('org.springframework.boot:spring-boot-starter-test') 
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

/* Install NPM and bower dependencies */
task installDeps(type: Exec) {
    executable "sh"
    args "-c", "npm install && bower install"
}

/* Execute gulp task and copy files from dist/ into static/ in JAR */
task gulp(type: Exec) {
    executable "sh"
    args "-c", "gulp default"
}
jar.into('static') {
    from('dist/')
}
jar.dependsOn gulp
